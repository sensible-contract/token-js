import "util.scrypt";
import "tokenProto.scrypt";
import "rabinUtil.scrypt";
import "protoheader.scrypt";

contract TokenGenesis {
  PubKey pubKey;

  public function unlock(
    SigHashPreimage txPreimage,
    Sig sig,
    bytes rabinMsg,
    bytes[RabinUtil.ORACLE_VERIFY_NUM] rabinPaddingArray,
    int[RabinUtil.ORACLE_VERIFY_NUM] rabinSigArray,
    int[RabinUtil.ORACLE_VERIFY_NUM] rabinPubKeyIndexArray,
    int[RabinUtil.ORACLE_VERIFY_NUM] rabinPubKeyArray,
    bytes rabinPubKeyHashArray,
    int genesisSatoshis,
    bytes tokenScript,
    int tokenSatoshis,
    Ripemd160 changeAddress,
    int changeSatoshis,
    bytes opReturnScript
    ) {

    require(Tx.checkPreimage(txPreimage));

    // check sig
    require(checkSig(sig, this.pubKey));

    // check input script oracle data
    bytes genesisScript = Util.scriptCode(txPreimage);
    int dataLen = len(genesisScript);
    bytes tokenName = TokenProto.getTokenName(genesisScript, dataLen);
    bytes tokenSymbol = TokenProto.getTokenSymbol(genesisScript, dataLen);
    int isGenesis = TokenProto.getGenesisFlag(genesisScript, dataLen);
    require(isGenesis == 1);
    int decimalNum = TokenProto.getDecimalNum(genesisScript, dataLen);
    bytes address = TokenProto.getTokenAddress(genesisScript, dataLen);
    require(address == b'0000000000000000000000000000000000000000');
    int tokenValue = TokenProto.getTokenAmount(genesisScript, dataLen);
    require(tokenValue == 0);
    require(TokenProto.getGenesisHash(genesisScript, dataLen) == b'0000000000000000000000000000000000000000');
    bytes sensibleID = TokenProto.getSensibleID(genesisScript, dataLen);
    bool isFirst = false;
    if (sensibleID == b'000000000000000000000000000000000000000000000000000000000000000000000000') {
      isFirst = true;
      sensibleID = Util.outpoint(txPreimage);
    } 
    int protoVersion = ProtoHeader.getProtoVersion(genesisScript, dataLen);
    require(TokenProto.PROTO_VERSION == protoVersion);
    int protoType = ProtoHeader.getProtoType(genesisScript, dataLen);
    require(protoType == TokenProto.PROTO_TYPE);
    bytes flag = ProtoHeader.getProtoFlag(genesisScript, dataLen);
    require(flag == TokenProto.PROTO_FLAG);
    // check opreturn
    require(TokenProto.checkDataLen(genesisScript, dataLen));
    require(TokenProto.checkOpReturn(genesisScript, dataLen));

    bytes rabinPubKeyHashArrayHash = TokenProto.getRabinPubKeyHashArrayHash(genesisScript, dataLen);
    // check the rabin msg
    if (!isFirst) {

      RabinUtil.verifyRabinPubKey(rabinPubKeyHashArrayHash,rabinPubKeyHashArray, rabinPubKeyArray, rabinPubKeyIndexArray);
      RabinUtil.verifyRabinMsg(rabinPubKeyArray, rabinMsg, rabinPaddingArray, rabinSigArray);

      bytes thisOutpoint = Util.outpoint(txPreimage);
      PayloadU4SpendByData pl = RabinUtil.loadPayloadU4SpendBy(rabinMsg);
      require(pl.byTxId == thisOutpoint[ : 32]);
      require(pl.outpoint == sensibleID || hash160(genesisScript) == pl.scriptHash);
    }

    // genesisHash
    bytes newGenesisScript = TokenProto.getNewGenesisScript(genesisScript, len(genesisScript), sensibleID);
    bytes genesisHash = hash160(newGenesisScript);

    // check tokenScript oracle data
    dataLen = len(tokenScript);

    require(tokenName == TokenProto.getTokenName(tokenScript, dataLen));
    require(tokenSymbol == TokenProto.getTokenSymbol(tokenScript, dataLen));
    require(0 == TokenProto.getGenesisFlag(tokenScript, dataLen));
    require(decimalNum == TokenProto.getDecimalNum(tokenScript, dataLen));
    require(sensibleID == TokenProto.getSensibleID(tokenScript, dataLen));
    require(ProtoHeader.getProtoVersion(tokenScript, dataLen) == TokenProto.PROTO_VERSION);
    protoType = ProtoHeader.getProtoType(tokenScript, dataLen);
    require(protoType == TokenProto.PROTO_TYPE);
    require(TokenProto.PROTO_FLAG == ProtoHeader.getProtoFlag(tokenScript, dataLen));
    require(rabinPubKeyHashArrayHash == TokenProto.getRabinPubKeyHashArrayHash(tokenScript, dataLen));
    require(genesisHash == TokenProto.getGenesisHash(tokenScript, dataLen));
    // check opreturn
    require(TokenProto.checkDataLen(tokenScript, dataLen));
    require(TokenProto.checkOpReturn(tokenScript, dataLen));

    bytes genesisOutput = b'';
    if (genesisSatoshis > 0) {
      genesisOutput = Util.buildOutput(newGenesisScript, genesisSatoshis);
    }
    bytes tokenOutput = Util.buildOutput(tokenScript, tokenSatoshis);

    // op_false op_return output
    bytes opReturnOutput = b'';
    if (len(opReturnScript) > 0) {
      require(opReturnScript[0:2] == b'006a');
      opReturnOutput = Util.buildOutput(opReturnScript, 0);
    }

    // bsv change output
    bytes changeOutput = TxUtil.genBsvOutput(changeSatoshis, changeAddress);

    bytes hashOutput = hash256(genesisOutput + tokenOutput + opReturnOutput + changeOutput);
    require(hashOutput == Util.hashOutputs(txPreimage));
  }
}